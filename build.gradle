buildscript {
    ext.buildConfig = [
        'compileSdk': 31,
        'minSdk'    : 23,
        'targetSdk' : 31,
    ]
	repositories {
		google()
        mavenCentral()
	}
	dependencies {
		classpath "com.android.tools.build:gradle:_"
		classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:_'
        classpath Google.playServicesGradlePlugin
        classpath Firebase.crashlyticsGradlePlugin

        classpath Google.dagger.hilt.android.gradlePlugin
	}
}

allprojects {
	repositories {
		google()
        mavenCentral()
	}
}

subprojects {
    configurations.configureEach {
        // We forcefully exclude AppCompat + MDC from any transitive dependencies.
        // This is a Compose app, so there's no need for these
        exclude group: 'androidx.appcompat'
        exclude group: 'com.google.android.material', module: 'material'
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            // Set JVM target to 11
            jvmTarget = JavaVersion.VERSION_11

            freeCompilerArgs += "-Xskip-prerelease-check"
            freeCompilerArgs += "-Xjvm-default=all"
            freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"

            // Enable experimental coroutines APIs, including Flow
            freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
            freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.FlowPreview"
            freeCompilerArgs += "-Xopt-in=kotlin.Experimental"

            // Treat all Kotlin warnings as errors
            allWarningsAsErrors = true
        }
    }
}

task clean(type: Delete) {
	delete rootProject.buildDir
}
